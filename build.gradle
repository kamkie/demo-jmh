buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.springframework:springloaded:1.2.4.RELEASE'
    }
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'application'

group 'net.devopssolutions'
version = getVersionName()
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile "org.apache.commons:commons-collections4:4.0"

    compile 'org.openjdk.jmh:jmh-core:1.3.+'
    compile 'org.openjdk.jmh:jmh-generator-annprocess:1.3.+'

    testCompile group: 'junit', name: 'junit'
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

bootRepackage.mustRunAfter installDist
build.dependsOn installDist